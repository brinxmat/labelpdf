group 'no.deichman'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'cpd'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

sourceSets.main.resources.srcDirs = [ "src/main" ]
sourceSets.main.resources.includes = [ "**/*.otf" ]

def version_junit = '4.11'
def version_itextpdf = '7.0.0'
def version_gson = '2.7'
def version_commons_cli = '1.3.1'
def version_commons_io = '2.4'
def version_slf4j = '1.7.21'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: version_junit
    compile group: 'com.itextpdf', name: 'io', version: version_itextpdf
    compile group: 'com.itextpdf', name: 'kernel', version: version_itextpdf
    compile group: 'com.itextpdf', name: 'layout', version: version_itextpdf
    compile group: 'com.itextpdf', name: 'barcodes', version: version_itextpdf
    compile group: 'com.google.code.gson', name: 'gson', version: version_gson
    compile group: 'commons-cli', name: 'commons-cli', version: version_commons_cli
    compile group: 'commons-io', name: 'commons-io', version: version_commons_io
    compile group: 'org.slf4j', name: 'slf4j-api', version: version_slf4j
    compile group: 'org.slf4j', name: 'slf4j-simple', version: version_slf4j
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

check.dependsOn test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Label PDF',
                'Implementation-Version': version,
                'Main-Class': 'no.deichman.labelpdf.LabelMain'
    }
    baseName = project.name + '-all'
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}


checkstyle {
    configProperties = [samedir: "${rootDir}"]
    toolVersion = 6.18
}

pmd {
    consoleOutput = true
    toolVersion = '5.4.1'
}

cpd {
    toolVersion = '5.3.3'
    ignoreIdentifiers = true
    ignoreLiterals = true

    ignoreFailures = true // not failing the build for now
}

cpdCheck {
    reports {
        text.enabled = true
        xml.enabled = false
    }
}

buildscript {
    dependencies {
        classpath 'de.aaschmid.gradle.plugins:gradle-cpd-plugin:0.5'
        classpath 'com.github.rholder:gradle-one-jar:1.0.4'
    }
    repositories {
        mavenCentral()
        maven {
            url '~/.m2/repository'
        }
        jcenter()
    }
}